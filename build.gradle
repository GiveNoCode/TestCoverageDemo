// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.0"
    ext.jacoco_version = "0.8.5"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = jacoco_version
}

task jacocoFullReport(type: JacocoReport, group: 'Coverage reports') {
    group = 'Verification'
    description = 'Generate Jacoco aggregate report for all modules'

    def projects = subprojects.findAll { it.getTasksByName("jacocoTestReport", false) }

    //noinspection GrUnresolvedAccess
    dependsOn(projects.jacocoTestReport)

    final source = files(projects.jacocoTestReport.sourceDirectories)

    additionalSourceDirs.setFrom source
    sourceDirectories.setFrom source

    classDirectories.setFrom files(projects.jacocoTestReport.classDirectories)
    executionData.setFrom files(projects.jacocoTestReport.executionData)

    reports {
        html {
            enabled true
            destination file("${buildDir}/reports/jacoco/html")
        }
    }

    doFirst {
        //noinspection GroovyAssignabilityCheck
        executionData.setFrom files(executionData.findAll { it.exists() })
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}